//******************************************************************************
// グローバル変数
//******************************************************************************
string sender;
string sender_saved;
string hwnd;
string ownedSSTPid;
string selfName;
string selfScreenName;
int balloonWidth;
array arHome;
array arHomeNext;
array arMentions;
array arMentionsNext;
array arLikes;
array arLocalTimeline;
string sHome;
string sMentions;
string sLikes;
string sMessages;
string sLocalTimeline;
dict dIconPath;
dict dEmojiPath;
dict dEmojiPathAlpha;
string postidToReply;
string reservedScript;
int isInit;
int BALLOON_WIDTH_MIN = 100;
//******************************************************************************
// プラグイン初期化処理
//******************************************************************************
loadPlugin()
{
	//デフォルトサーバーをセット
	if (dictSaveData["server_url"] == nil)
		dictSaveData += $("server_url", getDefaultMastodonServerURL());
	// reservedScriptを設定してOnGhostBootでnotifypluginを実行
	// sender, hwnd, ownedSSTPidを取得して後続のDSSTPを実行できるようにする
	isInit = 1;
	reservedScript = "\C\![notifyplugin," + pluginid() + ",OnMenuExec]\e";
}
TH_LoadPlugin()
{
	sender_saved = sender;
	// 定期投稿取得タイマセット
	string s = setTimerraise();
	if (s != "")
	{
		// 初回呼び出しは文字コードが正しく認識されない & logに記録されない (YAYAのバグ?)
		dict d = _saorirequest("yaya", "DSSTP.SendDummyRequestToYAYA");
		speakBySSTP("\C" + s + "\e", hwnd, ownedSSTPid);
		_sleep(1);
	}
	// 認証
	TH_VerifyAccount();
	_sleep(1);
	// 投稿全取得
	TH_GetAllToots();
	_sleep(1);
	// PLUGINの更新を確認
	TH_CheckUpdatePlugin();
}
//------------------------------------------------------------------------------
// PLUGINの更新を確認
TH_CheckUpdatePlugin()
{
	string url = homeurl() + "readme.txt";
	array arJson = httpget(url, "utf8");
	if (_aryvn(arJson) == 0)
		return;
	string versionNext;
	string reg = "^.*[0-9]+-[0-9]+-[0-9]+  Ver([0-9]\.[0-9])  .*$";
	for (int i = 0; i < _aryvn(arJson); i++)
	{
		if (_regex_match(arJson[i], reg))
			versionNext = _regex_search(arJson[i], reg)[1];
	}
	string versionNow = _strsplit(version(), "/")[1];
	if ((0.5 * versionNow) < (0.5 * versionNext))
	{
		speakBySSTP("\C\![raiseplugin," + pluginid() + ",OnShowPopUpUpdate," + versionNext + "]\e");
	}
}
string OnShowPopUpUpdate(dict ref)
{
	string versionNext = ref["Reference0"];
	string title = pluginname() + " 更新通知";
	string mes = pluginnameen() + "/" + versionNext + "がリリースされています";
	string s = showErrorMessage(title, mes, "info") + "\e";

	dictOptionalHeader["target"] = sender;
	dictOptionalHeader["script_option"] = "nobreak,notranslate";
	dictOptionalHeader["marker"] = version();

	return s;
}
string openUpdateMenu()
{
	string s;
	s += "\_l[0,0]■" + pluginnameen() + " update menu\_l[0,30]";
	s += menuItem("配布ページをブラウザで開く",
		{"UpdatePlugin", "OpenURL"}, 0);
	s += menuItem("ネットワーク更新を実行する",
		{"UpdatePlugin", "NetworkUpdate"}, 100);
	s += menuItem("キャンセル",
		{"Menu_CANCEL"}, 200);
	s  = "\0\_q" + s + "\_q\e";

	dictOptionalHeader["target"] = sender;
	dictOptionalHeader["script_option"] = "nobreak,notranslate";
	dictOptionalHeader["marker"] = version();

	return s;
}
//-------------------------------------------------------
// 選択肢：UpdatePlugin
// 内容　：プラグイン更新関連処理実行
//-------------------------------------------------------
string UpdatePlugin(dict ref)
{
	string s;
	switch (ref["Reference2"])
	{
	  case "OpenURL":
		s = "\C\0\_q \_q\![open,browser," + craftmanurl() + "]\e";
		break;
	  case "NetworkUpdate":
		s = "\![updateother,--plugin=" + pluginname() + "]\e";
		break;
	  default:
		break;
	}

	dictOptionalHeader["target"] = sender;
	dictOptionalHeader["script_option"] = "nobreak,notranslate";
	dictOptionalHeader["marker"] = version();

	return s;
}
string getnarurl()
{
	array arJson = httpget(githublatesturl(), "utf8");
	if (_aryvn(arJson) == 0)
		return "";
	dict dRes = _json2azv(arJson);
	string url = dRes["assets"][0]["browser_download_url"];
	return url;
}

//******************************************************************************
// プラグイン後始末処理
//******************************************************************************
unloadPlugin()
{
	// アイコンファイル削除
	array a = _fenum("icon")["file"];
	for (int i = 0; a[i] != nil; i++)
	{
		if (_regex_match(a[i], "^(icon|emoji|image)_.*\.png$"))
			_fdelete("icon\\" + a[i]);
	}
}
//******************************************************************************
// PLUGIN Event
//******************************************************************************
//-------------------------------------------------------
// 選択肢
//-------------------------------------------------------
string OnChoiceSelect(dict ref)
{
	return _fncstr(ref["Reference0"], ref);
}
string OnChoiceSelectEx(dict ref)
{
	return _fncstr(ref["Reference1"], ref);
}
//-------------------------------------------------------
// アンカー
//-------------------------------------------------------
string OnAnchorSelect(dict ref)
{
	string id = ref["Reference0"];
	string regurl = "^(https?://|www\.)[-_.!~*'()a-zA-Z0-9;/?:@&=+$,%#]+$";
	if (_regex_match(id, regurl)) {
		if (_regex_match(id, "^www\..+$")) {
			id = "http://" + id;
		}
		string url = escapeText(id);
		return "\C\![open,browser," + url + "]\_l[0,0]\_q \_q\e";
	} else {
		return _fncstr(id, ref);
	}
}
//-------------------------------------------------------
// ユーザ入力
//-------------------------------------------------------
string OnUserInput(dict ref)
{
	return _fncstr("Input" + ref["Reference0"], ref);
}
//-------------------------------------------------------
// イベント：OnExecuteHTTPComplete
// 内容　　：\![execute,http-post]成功
//-------------------------------------------------------
string OnExecuteHTTPComplete(dict ref)
{
	if (ref["Reference0"] != "post")
		return "";
	string s;
	switch (ref["Reference1"])
	{
	  case "MastodonPost":
		_create_thread("TH_UpdateHome");
		s = "\![set,balloontimeout,0]\0\b[2]\_q■updating...\_q\e";
		break;
	  case "MastodonAddStar":
		// 本来、保存してあるログを精査してfavのON/OFFを切り替える作業が入る
		// が、面倒なので全入れ替え
		_create_thread("TH_UpdateAll");
		s = "\![set,balloontimeout,0]\0\b[2]\_q■updating...\_q\e";
		break;
	  default:
		break;
	}
	dictOptionalHeader["target"] = sender;
	dictOptionalHeader["script_option"] = "nobreak,notranslate";
	dictOptionalHeader["marker"] = version();

	return s;
}
//-------------------------------------------------------
// イベント：OnExecuteHTTPFailure
// 内容　　：\![execute,http-post]失敗
//-------------------------------------------------------
string OnExecuteHTTPFailure(dict ref)
{
	if (ref["Reference0"] != "post")
		return "";
	string s = showErrorMessage(pluginname(), "failed to update post.", "warning") + "\e";

	dictOptionalHeader["target"] = sender;
	dictOptionalHeader["script_option"] = "nobreak,notranslate";
	dictOptionalHeader["marker"] = version();

	return s;
}
//-------------------------------------------------------
// イベント：OnTrayBalloonClick
//-------------------------------------------------------
string OnTrayBalloonClick(dict ref)
{
	string title = ref["Reference0"];
	string s;
	if (_strstr(title, pluginname() + " 新着") >= 0)
	{
		if (balloonWidth < 100)
			return "\![raiseplugin," + pluginid() + ",OnMenuExec]\e";
		else
			return showHomeTL();
	}
	if (_strstr(title, pluginname() + " 更新通知") >= 0)
	{
		return openUpdateMenu();
	}
	return "";
}
//-------------------------------------------------------
// イベント：OnGhostBoot(load時のみ予約Scriptを再生するために使用)
//-------------------------------------------------------
string OnGhostBoot(dict ref)
{
	if (reservedScript != "")
	{
		string s = reservedScript;
		reservedScript = "";
		dictOptionalHeader["script_option"] = "nobreak,notranslate";
		dictOptionalHeader["marker"] = version();
		return s;
	}
	return "";
}
//******************************************************************************
// 固有設定
//******************************************************************************
string getDefaultMastodonServerURL()
{
	return "https://ukadon.shillest.net/";
}
string getPluginOAuthURL()
{
	return "http://nikolat.starfree.jp/ukadonyu/";
}
string craftmanurl()
{
	return "http://nikolat.starfree.jp/ukadonyu/";
}
string githublatesturl()
{
	return "https://api.github.com/repos/nikolat/ukadonyu/releases/latest";
}
string homeurl()
{
	return "https://raw.githubusercontent.com/nikolat/ukadonyu/main/";
}
string pluginid()
{
	return "ba24f930-4b68-11ec-8072-0800200c9a66";
}
string pluginnameen()
{
	return "ukadonyu";
}
string pluginname()
{
	return "うかどんゅう";
}
string version()
{
	return "ukadonyu/2.2";
}
